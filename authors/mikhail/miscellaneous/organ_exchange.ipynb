{
  "cells": [
    {
      "attachments": {},
      "cell_type": "markdown",
      "metadata": {
        "id": "7RjB1ipC7KbS"
      },
      "source": [
        "# Organ exchange problem\n",
        "[![organ_exchange.ipynb](https://img.shields.io/badge/github-%23121011.svg?logo=github)](https://github.com/ampl/amplcolab/blob/master/authors/mikhail/miscellaneous/organ_exchange.ipynb) [![Open In Colab](https://colab.research.google.com/assets/colab-badge.svg)](https://colab.research.google.com/github/ampl/amplcolab/blob/master/authors/mikhail/miscellaneous/organ_exchange.ipynb) [![Kaggle](https://kaggle.com/static/images/open-in-kaggle.svg)](https://kaggle.com/kernels/welcome?src=https://github.com/ampl/amplcolab/blob/master/authors/mikhail/miscellaneous/organ_exchange.ipynb) [![Gradient](https://assets.paperspace.io/img/gradient-badge.svg)](https://console.paperspace.com/github/ampl/amplcolab/blob/master/authors/mikhail/miscellaneous/organ_exchange.ipynb) [![Open In SageMaker Studio Lab](https://studiolab.sagemaker.aws/studiolab.svg)](https://studiolab.sagemaker.aws/import/github/ampl/amplcolab/blob/master/authors/mikhail/miscellaneous/organ_exchange.ipynb) [![Hits](https://h.ampl.com/https://github.com/ampl/amplcolab/blob/master/authors/mikhail/miscellaneous/organ_exchange.ipynb)](https://colab.ampl.com)\n",
        "\n",
        "Description: Owing to the worldwide shortage of deceased-donor organs for transplantation, living donations have become a significant source of transplant organs. However, not all willing donors can\n",
        "donate to their intended recipients because of medical incompatibilities. These incompatibilities can be overcome by an exchange of donors between patients. \n",
        "\n",
        "Tags: mip\n",
        "\n",
        "Model author: Mikhail Riabtsev\n",
        "\n",
        "Notebook author: Mikhail <<mail@solverytic.com>>\n",
        "\n",
        "References: https://sites.bc.edu/utku-unver/wp-content/uploads/sites/67/2019/11/slides-dual-donor-exchange-2016.pdf\n",
        "https://eml.berkeley.edu/~hie/research/dual-donor-organ-exchange.pdf"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "metadata": {
        "id": "rQ50dBwL7KbX"
      },
      "outputs": [],
      "source": [
        "# Install dependencies\n",
        "!pip install -q amplpy"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "metadata": {
        "colab": {
          "base_uri": "https://localhost:8080/"
        },
        "id": "s7DEaRp87KbY",
        "outputId": "6ece6d53-a3db-443c-c623-5cc409115fdf"
      },
      "outputs": [],
      "source": [
        "# Google Colab & Kaggle integration\n",
        "from amplpy import AMPL, tools\n",
        "ampl = tools.ampl_notebook(\n",
        "    modules=[\"highs\"], # modules to install\n",
        "    license_uuid=\"default\", # license to use\n",
        "    g=globals()) # instantiate AMPL object and register magics"
      ]
    },
    {
      "cell_type": "markdown",
      "metadata": {
        "id": "twrd0pLo7KbZ"
      },
      "source": [
        "## Model"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "metadata": {
        "id": "aEtyesqs7KbZ"
      },
      "outputs": [],
      "source": [
        "%%ampl_eval\n",
        "# Donor Exchange\n",
        "reset;\n",
        "### SETS \n",
        "  param N;\n",
        "  set Blood;                      \t\t\t\t\t# set of blood types (b)\n",
        "  set PATIENTS := {1..N} ordered;   \t\t\t\t# set of patients (i)\n",
        "  set DONORS;                     \t\t\t\t\t# set of donors (j)\n",
        "  set ALL:= PATIENTS union DONORS;\t\t\t\t\t# set of patients union donors (ij)\n",
        "  set Transpl;                    \t\t\t\t\t# type of transplant {tr}\n",
        "  set BLOOD within {Blood, Blood};\t\t\t\t\t# possible options for the donation of blood type\n",
        "  set PAT_DON within {PATIENTS, DONORS}; \t\t\t# donor-patient relationship\n",
        "  set TRANSPL_HUM within {ALL,Transpl}; \t\t\t# type of transplant assigned to a PATIENTS and DONORS\n",
        "  set BLOOD_HUM within {ALL, Blood}; \t\t\t\t# PATIENTS and DONORS blood type\n",
        "  # patients data:  patient(i),type of transplant(tr), blood type(b) \n",
        "  set PATIENTS_:= setof {(i,tr) in TRANSPL_HUM,\t(i,b) in BLOOD_HUM: i in PATIENTS} (i,tr,b);\n",
        "  # donors data: relationship between patient(i) & donor(j), possible type of transplant(tr), blood type(b) \t\n",
        "  set DONORS_:= setof {(j,tr) in TRANSPL_HUM, (j,b) in BLOOD_HUM, (i,j) in PAT_DON:j in DONORS}(i,j,tr,b);\n",
        "  ### a set of options for possible assignments: \n",
        "  \t# donor(j) of the patient(ii) who can be assigned to the patient(i) as it is compatible in type of transplantation(tr) and terms of blood type(b1 and b2) \n",
        "  set LINKS_:= setof {(ii,tr,b1) in PATIENTS_, (i,j,tr,b2) in DONORS_:(b1,b2) in BLOOD}(i,j,ii,tr,b1);\n",
        "  ### a set of options for possible assignments:\n",
        "  \t# donor(j) of the patient(ii) who can be assigned to the patient(i) as it is compatible in type of transplantation(tr) and terms of blood type(b1 and b2) \n",
        "  set LINKS:= setof {(ii,tr,b1) in PATIENTS_, (i,j,tr,b2) in DONORS_:(b1,b2) in BLOOD}(i,j,ii,tr,b1,b2);\n",
        "  ### a set of options for possible assignments TWO donors for ONE patient:\n",
        "  \t# a patient (i) with a blood type (b1) can receive 2 parts of the transplant (tr) from donors (j1 and j2) with a compatible blood type (b2, b3)\n",
        "  set PAT_2DON:= setof {(i,j1,ii,tr,b1,b2) in LINKS, (i,j2,tr,b3) in DONORS_:(b1,b3) in BLOOD and (b2,b3) in BLOOD and j1 != j2} (i,j1,j2,tr,b1,b2,b3);\t\t\t\t\t\t\t\t\t\n",
        "\t### To simplify the model, the compatibility of donors and patients is checked only by blood type. If necessary, \n",
        "\t# you can add a check of additional criteria for the compatibility of donors and patients. ###\n",
        "\n",
        "### PARAMETERS\n",
        "\tparam Clusters:= last(PATIENTS);\t\t\t\t# create a transplantation cluster for each patient. The introduction of the cluster guarantees simultaneous operation for the donor and his patient.\n",
        "\tparam demand {(i,tr,b) in PATIENTS_} >= 0; \t\t# required number of surgeries for a patient\n",
        "\n",
        "### VARIABLES\n",
        "\tvar X {PATIENTS, 1..Clusters} binary;   \t\t# all operations on the patient must be performed within the same cluster\n",
        "\tvar Y {LINKS_, 1..Clusters} binary;      \t\t# the donor participates in only one cluster\n",
        "\n",
        "### OBJECTIVE\n",
        "\t# Maximize the number of transplants\t\t\n",
        "\tmaximize Total: sum {i in PATIENTS, c in 1..Clusters} X [i,c];    \n",
        "\n",
        "### REQUAREMENTS\n",
        "\tsubject to\n",
        "\t# for each cluster, supply - demand >= 0;\n",
        "\tA {c in 1..Clusters,tr in Transpl, b in Blood}: sum{(i,j,ii,tr,b) in LINKS_} Y[i,j,ii,tr,b,c] - sum {(i,tr,b) in PATIENTS_} demand[i,tr,b] * X[i,c] >= 0; # you can put =\n",
        "\t# a patient can only be part of one cluster\n",
        "\tA_1 {i in PATIENTS}: sum {c in 1..Clusters} X[i,c] <= 1;\n",
        "\t# for each type of operation of the patient in all clusters, only one donor is assigned.\n",
        "\tA_2 {ii in PATIENTS, tr in Transpl}:sum{c in 1..Clusters, (i,j,ii,tr,b) in LINKS_} Y[i,j,ii,tr,b,c] <= 1;\n",
        "\t# Limiting the number of patients in 1 cluster.\n",
        "\tA_3 {c in 1..Clusters}: sum {i in PATIENTS} X[i,c] <= 3; \n",
        "\t# A donor can participate in an organ transplant operation only 1 time \n",
        "\tA_4 {j in DONORS, tr in Transpl}: sum{c in 1..Clusters, (i,j,ii,tr,b) in LINKS_} Y[i,j,ii,tr,b,c] <= 1;\n",
        "\t# Limiting the number of operations in the cluster (it is assumed that after assigning a donor for transplantation, another operation is performed with the patient)\n",
        "\tA_5 {c in 1..Clusters}: sum{(i,j,ii,tr,b) in LINKS_} Y[i,j,ii,tr,b,c]*2 <= 6;\n",
        "\t\n"
      ]
    },
    {
      "attachments": {},
      "cell_type": "markdown",
      "metadata": {
        "id": "jCyp8qRS7Kba"
      },
      "source": [
        "## Data of model"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "metadata": {
        "id": "jYDiBDD_7Kba"
      },
      "outputs": [],
      "source": [
        "%%ampl_eval\n",
        "### DATA\n",
        "\tdata;\n",
        "  param N:=5;\t\t\t\t\t\t\t\t\t\t\t\t# number of patients\n",
        "  set Blood := O, A, B, AB;\t\t\t\t\t\t\t\t\t# types of blood\n",
        "  set DONORS := d01 d02 d03 d04 d05 d06 d07 d08 d09 d10 ; \t# number of donors\n",
        "  set Transpl := Liver, Kidney, Lung;\t\t\t\t\t\t# Type of transplants\n",
        "  set BLOOD: O   A   B  AB := \t\t\t\t\t\t\t\t# Data on compatibility of blood types\n",
        "\tO          +   -   -  -\n",
        "\tA          +   +   -  -\n",
        "\tB          +   -   +  -\n",
        "\tAB         +   +   +  + ;\n",
        "  set PAT_DON(tr): \t\t\t\t\t\t\t\t\t\t\t# Donor affiliation with patients\n",
        "\t\t1   2   3   4   5 :=\n",
        "\td01 -   +   -   -   -\n",
        "\td02 -   +   -   -   -   \n",
        "\td03 +   -   -   -   -\n",
        "\td04 +   -   -   -   -\n",
        "\td05 -   -   +   -   -\n",
        "\td06 -   -   -   -   +\n",
        "\td07 -   -   -   +   -\n",
        "\td08 -   -   -   -   +\n",
        "\td09 -   -   +   -   -\n",
        "\td10 +   -   -   -   -  ;\n",
        "  set TRANSPL_HUM: Liver  Kidney  Lung:=\n",
        "\td01              +      -       +       # This table assumes that each patient is operated \n",
        "\td02              -      -       +       # on in one approach, i.e. all the necessary \n",
        "\td03              +      -       -       # operations are performed at 1 time.\n",
        "\td04              +      -       +      \n",
        "\td05              -      +       -      \n",
        "\td06              +      -       -      \n",
        "\td07              +      +       +      \n",
        "\td08              -      +       -      \n",
        "\td09              +      -       -      \n",
        "\td10              +      -       +      \n",
        "\t1                -      -       +      \n",
        "\t2                +      +       +      \n",
        "\t3                +      -       -      \n",
        "\t4                +      +       -      \n",
        "\t5                +      -       -     ; # This condition means and. You could change to or.\n",
        "  set BLOOD_HUM:   O   A   B   AB:=\t\t\t# PATIENTS and DONORS blood type\n",
        "\td01              +   -   -   -    \n",
        "\td02              -   +   -   -      \n",
        "\td03              -   -   +   -      \n",
        "\td04              -   +   -   -      \n",
        "\td05              -   -   -   +      \n",
        "\td06              -   -   -   +      \n",
        "\td07              -   +   -   -      \n",
        "\td08              +   -   -   -     \n",
        "\td09              +   -   -   -      \n",
        "\td10              -   -   -   +      \n",
        "\t1                +   -   -   -    \n",
        "\t2                -   +   -   -      \n",
        "\t3                -   -   -   +     \n",
        "\t4                -   +   -   -      \n",
        "\t5                +   -   -   -     ; \n",
        "\n",
        "param demand default 1;\t\t\t\t\t\t# required number of surgeries for a patient\n"
      ]
    },
    {
      "cell_type": "markdown",
      "metadata": {
        "id": "uftSUdTh9eZY"
      },
      "source": [
        "## Solve with Highs"
      ]
    },
    {
      "cell_type": "code",
      "execution_count": null,
      "metadata": {
        "colab": {
          "base_uri": "https://localhost:8080/"
        },
        "id": "j6pPuSOi8nPH",
        "outputId": "0f6ad246-3351-4187-b373-216ec7b36c98"
      },
      "outputs": [
        {
          "name": "stdout",
          "output_type": "stream",
          "text": [
            "HiGHS 1.5.1: \b\b\b\b\b\b\b\b\b\b\b\b\bHiGHS 1.5.1: optimal solution; objective 5\n",
            "36 simplex iterations\n",
            "1 branching nodes\n",
            "set BLOOD :=\n",
            "(O,O)     (A,A)     (B,B)     (AB,A)    (AB,AB)\n",
            "(A,O)     (B,O)     (AB,O)    (AB,B);\n",
            "\n",
            "set PAT_2DON :=\n",
            "(2,d02,d01,Lung,A,A,O)      (1,d10,d04,Liver,AB,AB,A)\n",
            "(1,d10,d03,Liver,AB,AB,B);\n",
            "\n",
            "X [*,*]\n",
            ":   1   2   3   4   5    :=\n",
            "1   0   0   0   0   1\n",
            "2   1   0   0   0   0\n",
            "3   0   0   0   0   1\n",
            "4   0   0   1   0   0\n",
            "5   0   1   0   0   0\n",
            ";\n",
            "\n",
            "Y [1,*,2,Liver,A,*] (tr)\n",
            ": d04    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [1,*,2,Lung,A,*] (tr)\n",
            ": d04    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [1,*,3,Liver,AB,*] (tr)\n",
            ": d03 d04 d10    :=\n",
            "1   0   0   0\n",
            "2   0   0   0\n",
            "3   0   0   0\n",
            "4   0   0   0\n",
            "5   1   0   0\n",
            "\n",
            " [1,*,4,Liver,A,*] (tr)\n",
            ": d04    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [2,*,1,Lung,O,*] (tr)\n",
            ": d01    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   1\n",
            "\n",
            " [2,*,2,Liver,A,*] (tr)\n",
            ": d01    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [2,*,2,Lung,A,*] (tr)\n",
            ": d01 d02    :=\n",
            "1   0   1\n",
            "2   0   0\n",
            "3   0   0\n",
            "4   0   0\n",
            "5   0   0\n",
            "\n",
            " [2,*,3,Liver,AB,*] (tr)\n",
            ": d01    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [2,*,4,Liver,A,*] (tr)\n",
            ": d01    :=\n",
            "1   1\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [2,*,5,Liver,O,*] (tr)\n",
            ": d01    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [3,*,2,Liver,A,*] (tr)\n",
            ": d09    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [3,*,3,Liver,AB,*] (tr)\n",
            ": d09    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [3,*,4,Liver,A,*] (tr)\n",
            ": d09    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [3,*,5,Liver,O,*] (tr)\n",
            ": d09    :=\n",
            "1   0\n",
            "2   1\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [4,*,2,Kidney,A,*] (tr)\n",
            ": d07    :=\n",
            "1   0\n",
            "2   0\n",
            "3   1\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [4,*,2,Liver,A,*] (tr)\n",
            ": d07    :=\n",
            "1   0\n",
            "2   0\n",
            "3   1\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [4,*,2,Lung,A,*] (tr)\n",
            ": d07    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [4,*,3,Liver,AB,*] (tr)\n",
            ": d07    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [4,*,4,Kidney,A,*] (tr)\n",
            ": d07    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [4,*,4,Liver,A,*] (tr)\n",
            ": d07    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [5,*,2,Kidney,A,*] (tr)\n",
            ": d08    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [5,*,3,Liver,AB,*] (tr)\n",
            ": d06    :=\n",
            "1   0\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            "\n",
            " [5,*,4,Kidney,A,*] (tr)\n",
            ": d08    :=\n",
            "1   1\n",
            "2   0\n",
            "3   0\n",
            "4   0\n",
            "5   0\n",
            ";\n",
            "\n"
          ]
        }
      ],
      "source": [
        "%%ampl_eval\n",
        "\n",
        "### OPTIONS  \n",
        "\toption solver highs;\n",
        "\t\n",
        "### SOLVE\t\n",
        "\tsolve;\n",
        "\t\n",
        "### RESULTS\t\n",
        "\tdisplay BLOOD, PAT_2DON;\n",
        "\tdisplay X, Y;"
      ]
    }
  ],
  "metadata": {
    "colab": {
      "provenance": []
    },
    "kernelspec": {
      "display_name": "Python 3.9.13 ('venv': venv)",
      "language": "python",
      "name": "python3"
    },
    "language_info": {
      "codemirror_mode": {
        "name": "ipython",
        "version": 3
      },
      "file_extension": ".py",
      "mimetype": "text/x-python",
      "name": "python",
      "nbconvert_exporter": "python",
      "pygments_lexer": "ipython3",
      "version": "3.9.13 (main, May 24 2022, 21:28:31) \n[Clang 13.1.6 (clang-1316.0.21.2)]"
    },
    "vscode": {
      "interpreter": {
        "hash": "ee4198aac13dae9b6fccd443041680d99c2643c4956ef7c80ff6dcc0057cb523"
      }
    }
  },
  "nbformat": 4,
  "nbformat_minor": 0
}
